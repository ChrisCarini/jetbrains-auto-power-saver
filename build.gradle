buildscript {
  ext {
    javaVersion = '11'

    // see https://www.jetbrains.com/intellij-repository/releases/
    // and https://www.jetbrains.com/intellij-repository/snapshots/
    // To use/download EAP add '-EAP-SNAPSHOT' to the version, i.e. 'IU-191.6014.8-EAP-SNAPSHOT'
    intellijVersion = '2021.1'
//    intellijVersion = '201.6668.60-EAP-SNAPSHOT'
  }
}

plugins {
  id 'java'
  id 'org.jetbrains.intellij' version '0.7.2'
}

group 'com.chriscarini.jetbrains'
version '2.4.0'

sourceCompatibility = javaVersion

repositories {
  mavenCentral()
}

test {
  useTestNG()
  useJUnit()
}

dependencies {
  testCompile 'org.testng:testng:6.1.1'
  testCompile "org.mockito:mockito-core:2.18.0"
}

intellij {
  version intellijVersion
  plugins[]
  pluginName 'Automatic Power Saver'

  // TODO (https://youtrack.jetbrains.com/issue/IDEA-252693) - Disabled this task because it was failing. See my
  //      comment on the above YouTrack issue, and thread here: https://jetbrains-platform.slack.com/archives/C5U8BM1MK/p1604722479347300?thread_ts=1602338567.267100&cid=C5U8BM1MK
  // Note: This gradle task is only needed if `SearchableConfigurable` is used,
  //       not `Configurable`.
  //       See the following comment for details: https://jetbrains-platform.slack.com/archives/C5U8BM1MK/p1606813009234800?thread_ts=1606794793.231900&cid=C5U8BM1MK
  buildSearchableOptions {
    enabled = false
  }
}

//runIde {
//  jbrVersion 'jbrex8u202b1483.21'
//}

patchPluginXml {
  changeNotes """
        <h4>[$version] - ${new Date().format('yyyy-MM-dd')}</h4>
        <h5>Upgrade</h5>
        <ol>
        <li>Upgrading to 2021.1</li>
        <li>Upgrading IntelliJ Gradle plugin to 0.7.2</li>
        </ol>
        <h5>Added</h5>
        <ol>
        <li>N/A</li>
        </ol>
        <h5>Fixed</h5>
        <ol>
        <li>N/A</li>
        </ol>
        """
}

// The below will surface warnings as errors - I want
// this in the project to allow us to catch deprecation
// notices early and often.
tasks.withType(JavaCompile){
  options.compilerArgs.addAll(['-Xlint:all',
                               '-Xlint:-options',
                               '-Xlint:-rawtypes',
                               '-Xlint:-processing',
                               '-Xlint:-path', // Ignore JBR SDK manifest element warnings
                               '-proc:none',
                               '-Werror',
                               '-Xlint:-classfile']) //ignore warnings from dependencies
}

if (System.getenv('IS_CI') != "True") {
  // The below file (jetbrainsCredentials.gradle) should contain the below:
  //    project.ext.set("intellijPublishUsername", "YOUR_USERNAME_HERE")
  //    project.ext.set("intellijPublishPassword", "YOUR_PASSWORD_HERE")
  //    project.ext.set("intellijPluginPublishToken", "YOUR_TOKEN_HERE")
  //
  // Because this contains the username and password, this file is also included in .gitignore file.
  apply from: 'jetbrainsCredentials.gradle'

  publishPlugin {
//    username intellijPublishUsername
//    password intellijPublishPassword
    token intellijPluginPublishToken
  }

  task checkJetBrainsCreds {
    doLast {
      println "Username: " + intellijPublishUsername
      println "Password " + (project.hasProperty('intellijPublishPassword') ? "IS" : "is NOT") + " set."
    }
  }
}
